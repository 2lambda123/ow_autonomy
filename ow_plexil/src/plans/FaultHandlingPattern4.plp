// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Pattern summary: Execute a sequence of actions. If a fault is injected
// during a related action, the plan will stop the action. If/when the fault
// is cleared, the action will automatically be attempted again.

// More specifically: If no faults are occurring, this plan will pan 180 
// degrees, and then unstow and stow the arm.
// If any fault is injected, the plan will pause and wait for resolution.
// If a fault is injected during an operation, that operation will only
// halt immediately if the fault injected is associated with it directly,
// i.e. an antenna fault during panning or an arm fault during 
// unstowing or stowing.
// Otherwise, the pausing will visually become apparent when the next
// operation does not begin due to it.

#include "plan-interface.h"

LibraryAction InitializeAntennaAndArm ();

FaultHandlingPattern4:
{

  Boolean MissionInProgress = true;

  Boolean PanDone = false;
  Boolean UnstowDone = false;
  Boolean StowDone = false;

  log_info ("Starting FaultHandlingPattern4 plan...");

  LibraryCall InitializeAntennaAndArm ();

  RunMission: Concurrence
  {
    
    End !MissionInProgress;
    log_info ("Starting Mission...");

    LogPanError:
    {

      Start !PanDone && Lookup(AntennaFault);
    
      log_error ("Panning halted due to an antenna fault.");
    }

    LogUnstowError:
    {

      Start !UnstowDone && Lookup(ArmFault);
    
      log_error ("Unstowing halted due to an arm fault.");
    }

    LogStowError:
    {

      Start UnstowDone && !StowDone && Lookup(ArmFault);
    
      log_error ("Stowing halted due to an arm fault.");
    }

    Mission: 
    {
    
      PanAntenna:
      {
        Real PanToleranceDegrees = 1.0;

        Repeat !PanDone;
        Start !Lookup(AntennaFault);
        Skip PanDone;

        LibraryCall Pan (Degrees=180);

        if abs(180 - abs(Lookup(PanDegrees))) < PanToleranceDegrees
        {
          PanDone = true;
        }

      }

      UnstowArm:
      {
        Repeat !UnstowDone;
        Start !Lookup(ArmFault);
        Skip UnstowDone;

        LibraryCall Unstow(); 
      
        // Since it is currently not possible to check the success/failure
        // of lander commands or the current arm position, we instead
        // use the following temporary check for when an arm fault is resolved
        // to denote that the arm has been unstowed.

        // The assumption is that if the above Unstow() has completed
        // and there is no arm fault, unstow is most likely successfully done.

        // Limitation: If the arm fault is cleared before Unstow() finishes,
        // successfully or otherwise, this check will incorrectly indicate
        // completion of unstowing altogether.

        UnstowDone = !Lookup(ArmFault);

      }

    
      StowArm:
      {
        Repeat !StowDone;
        Start !Lookup(ArmFault);
        Skip StowDone;

        LibraryCall Stow();

        // Since it is currently not possible to check the success/failure
        // of lander commands or the current arm position, we instead
        // use the following temporary check for when an arm fault is resolved
        // to denote that the arm has been stowed.

        // The assumption is that if the above Stow() has completed
        // and there is no arm fault, unstow is most likely successfully done.

        // Limitation: If the arm fault is cleared before Stow() finishes,
        // successfully or otherwise, this check will incorrectly indicate
        // completion of stowing altogether.
      
        StowDone = !Lookup(ArmFault);
  
      }

      MissionInProgress = false;
    }

  }

  log_info ("FaultHandlingPattern4 plan complete.");

}
