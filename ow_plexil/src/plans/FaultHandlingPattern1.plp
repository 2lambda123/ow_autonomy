// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Pan in 15 degree increments as long as no antenna fault is present.
// If an antenna fault is occurs, pause until the fault is removed.

#include "plan-interface.h"

LibraryAction InitializeAntennaAndArm (InOut Boolean initialize);

FaultHandlingPattern1:
{
  Boolean Initialize = true;
  Real NewAngle = 0;

  log_info ("Starting FaultHandlingPattern1 plan...");

  LibraryCall InitializeAntennaAndArm (initialize = Initialize);

  Pattern1Image:
  {
    Repeat true;
    Start !Lookup(AntennaFault);

    NewAngle = Lookup(PanDegrees) + 15;

    if NewAngle > 360
    {
      NewAngle = NewAngle mod 360;
    }
    endif;

    SynchronousCommand pan_antenna (NewAngle);
  }

  log_info ("FaultHandlingPattern1 plan complete.");
}

