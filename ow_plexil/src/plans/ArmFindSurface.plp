// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Perform an ArmFindSurface as specified.

#include "plan-interface.h"

ArmFindSurface:
{
  In Integer Frame;
  In Boolean Relative;
  In Real PosX;
  In Real PosY;
  In Real PosZ;
  In Real NormX;
  In Real NormY;
  In Real NormZ;
  In Real Distance;
  In Real Overdrive;
  In Real ForceThreshold;
  In Real TorqueThreshold;

  Boolean FaultDetected = false;

  PostCondition !Lookup(ArmFault);

  if Lookup(ArmFault)
  {
    log_error ("Command arm_find_surface not sent ",
	       "to lander due to active arm fault(s).");
    FaultDetected = true;
  }

  SendArmFindSurface:
  {
    Start !Lookup(ArmFault);

    if FaultDetected
    {
      log_info ("Arm fault(s) resolved, ",
		"sending arm_find_surface command to lander...");
    }

    SynchronousCommand arm_find_surface (Frame, Relative, PosX, PosY, PosZ,
                                         NormX, NormY, NormZ,
                                         Distance, Overdrive,
                                         ForceThreshold, TorqueThreshold);
  }
}
