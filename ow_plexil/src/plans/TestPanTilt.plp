// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Simple plan to test Pan and Tilt functions

#include "plan-interface.h"

TestPanTilt:
{

    In Real Goal;
    In String Action;
    InOut Integer TestSuccess;
    Real tolerance = 2.865;  // matches 0.05R used in simulator
    Real current = 0;

    //output test number
    log_info("TEST: ", TestSuccess+1);

    //check for which command was passed
    if(Action == "Pan" || Action == "pan"){
      LibraryCall Pan(Degrees = Goal);
      current = Lookup(PanDegrees);
    }
    elseif(Action == "Tilt" || Action == "tilt"){
      LibraryCall Tilt(Degrees = Goal);
      current = Lookup(TiltDegrees);
    }
    endif;

    //check if resulting position is within tolerance
    //    if(abs(current - Goal) <= tolerance){
    if (Lookup (AnglesEquivalent(current, Goal, tolerance))) {
      log_info(Action, ": SUCCEEDED", " Goal: ", Goal, " Actual: ", current);
      TestSuccess = TestSuccess + 1;
    }
    else{
      log_error(Action, ": FAILED", " Goal: ", Goal, " Actual: ", current);
    }
    endif;
}
