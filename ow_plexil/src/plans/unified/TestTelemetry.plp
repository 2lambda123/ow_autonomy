// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Read and print telemetry values.

#include "unified-interface.h"

TestTelemetry:
{
  Real FT[6]; // end effector force/torque
  Real ArmPose[7]; // Position (x,y,z) followed by orientation (x,y,z,w).

  // Pan/Tilt telemetry.

  // Note that the current version of OWLAT Sim has neither a pan/tilt
  // command nor pan/tilt telemetry.  Unless values are published to
  // /pan_tilt_position, these lookups will return the PLEXIL
  // adapter's default Lookup value, which is 0, when this plan is run
  // on OWLAT Sim.

  log_info ("Pan degees: ", Lookup(PanDegrees));
  log_info ("Pan radians: ", Lookup(PanRadians));
  log_info ("Tilt degees: ", Lookup(TiltDegrees));
  log_info ("Tilt radians: ", Lookup(TiltRadians));

  // Joint telemetry

  // This is coded for the 9 joints in OceanWATERS (7 arm, 2 antenna),
  // while OWLAT has a total of 6 joints, all arm.  Nonetheless, this
  // code will do the "right" thing on each testbed, which includes
  // producing warnings/errors for invalid indices and options.

  for (Integer j = 0; j < 9; j + 1) {
    log_info ("Position of joint ", j, " = ", Lookup(JointPosition(j)));
    log_info ("Velocity of joint ", j, " = ", Lookup(JointVelocity(j)));
    log_info ("Effort of joint ", j, " = ", Lookup(JointEffort(j)));
    log_info ("Acceleration of joint ", j, " = ", Lookup(JointAcceleration(j)));
  }

  FT = Lookup(ArmEndEffectorForceTorque);
  log_info ("Arm end effector force/torque: {");
  for (Integer i = 0; i < 6; i + 1) {
    log_info ("  ", FT[i]);
  }

  ArmPose = Lookup(ArmPose);
  log_info ("Arm pose: {");
  for (Integer i = 0; i < 7; i + 1) {
    log_info ("  ", ArmPose[i]);
  }
}
