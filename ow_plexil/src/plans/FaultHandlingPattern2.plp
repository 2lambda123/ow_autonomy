// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Action 1: Pan antenna in 15 degree increments.
// Action 2: Unstow and stow the arm repeatedly.
// If no faults are occurring, action 1 and 2 occur simultaneously.
// If an antenna fault but no arm faults occurring, only action 2 occurs.
// If an arm fault but no antenna faults occurring, only action 1 occurs.
// If both antenna and arm faults occurring, the plan waits for resolution.


#include "plan-interface.h"

LibraryAction InitializeAntennaAndArm (InOut Boolean initialize);

FaultHandlingPattern2: Concurrence
{
  Boolean Initialize = true;
  Real NewAngle = 0;

  log_info ("Starting FaultHandlingPattern2 plan...");

  LibraryCall InitializeAntennaAndArm (initialize = Initialize);

  Pattern2Image:
  {
    Repeat true;
    Start !Lookup(AntennaFault) && !Initialize;

    NewAngle = Lookup(PanDegrees) + 15;

    if NewAngle > 360
    {
      NewAngle = NewAngle mod 360;
    }
    endif;

    SynchronousCommand pan_antenna (NewAngle);
  }

  Pattern2UnstowStow:
  {
    Repeat true;
    Start !Lookup(ArmFault) && !Initialize;

    SynchronousCommand unstow();
    SynchronousCommand stow();
      
  }

}
