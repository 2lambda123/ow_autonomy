Real Lookup time;
Real Lookup TiltDegrees;
Command tilt_antenna (Real);
Command log_info (...);
Command log_error (...);

Tilt:
{
  In Real Degrees;

  Real Tolerance   = 0.001;  // degrees, made up, tweak as needed
  Real TiltTimeout = 5.0;    // seconds, made up, tweak as needed

  // Possible todo: add preconditions to check for resource availability, etc.

  // Send command and assume success for now.  Later, implement command handles
  // to check for successful processing of the command.
  //
  tilt_antenna (Degrees);

  WaitForCompletion:
  {
    EndCondition ((abs(Lookup(TiltDegrees) - Degrees) < Tolerance) ||
                  Lookup(time) - WaitForCompletion.EXECUTING.START > TiltTimeout);
  }

  if (Lookup(time) - WaitForCompletion.EXECUTING.START > TiltTimeout) {
    log_error ("Tilt timed out");
  }
  else log_info ("Tilt reached ", Lookup(TiltDegrees));
  endif;
}
