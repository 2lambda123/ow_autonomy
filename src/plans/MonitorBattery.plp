// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// An overly simple battery monitor that detects voltage drop below a fixed
// threshold.  Supports recharge, which the Europa lander does not.

Real Lookup StateOfCharge;
Command log_info (...);
Command log_warning (...);

MonitorBattery:
{
  In Boolean continue;
  InOut Boolean status;

  Real MinStateOfCharge = 0.10;  // made up

  MonitorBatteryAux: Concurrence
  {
    Repeat true;
    Exit !continue;

    BatteryGood:
    {
      Start Lookup(StateOfCharge) >= MinStateOfCharge;
      status = true;
      log_info ("State of charge normal:", Lookup(StateOfCharge));
    }

    BatteryBad:
    {
      Start Lookup(StateOfCharge) < MinStateOfCharge;
      status = false;
      log_warning ("Low voltage:", Lookup(StateOfCharge));
    }
  }
  log_info ("Battery Monitor terminating.");
}
