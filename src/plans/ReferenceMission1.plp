// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Simplified version of the Europa reference mission.

LibraryAction ImageLandingSite(In String InstanceName,
                               In Boolean IgnoreCrash);
LibraryAction InterogateSurface;
LibraryAction IdentifySampleTarget (InOut Real X, InOut Real Y,
                                    InOut Real GroundPos);
LibraryAction DigTrench (In Real X, In Real Y, In Real GroundPos);
LibraryAction CollectSample;
LibraryAction StartSampleAnalysis;
LibraryAction Stub(In String desc);
LibraryAction MonitorBattery (InOut Boolean status);

Command log_info (...);

ReferenceMission1: Concurrence
{
  // Assumption: checkout is complete.

  // Health variables - expand/refine as needed
  Boolean BatteryOK, TemperatureOK;

  ContingencyMonitors: Concurrence
  {
    BatteryMonitor: LibraryCall MonitorBattery (status = BatteryOK);
    ThermalMonitor: LibraryCall Stub(desc="Monitoring thermal");
    UplinkMonitor: LibraryCall Stub(desc="Monitoring for uplinks");
  }

  Mission:
  {
    Real trench_x, trench_y, ground_pos;

    log_info ("Starting Reference Mission 1, Sol 0 ...");
    LibraryCall ImageLandingSite (InstanceName = "ReferenceMission1",
                                  IgnoreCrash = true);
    LibraryCall IdentifySampleTarget (X = trench_x,
                                      Y = trench_y,
                                      GroundPos = ground_pos);
    LibraryCall DigTrench (X = trench_x, Y = trench_y, GroundPos = ground_pos);
    LibraryCall CollectSample;
    LibraryCall StartSampleAnalysis;
    log_info ("Reference Mission 1, Sol 0 complete.");
  }
}
