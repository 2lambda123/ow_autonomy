// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

LibraryAction CompatibleTime(In Real First,
                 In Real Second,
                 In String CheckpointName,
                 In String Args,
                 InOut Boolean Result);

Real Lookup CheckpointTime(...);
Integer Lookup CheckpointWhen(String);
String Lookup CheckpointInfo(...);
Boolean Lookup DidCrash;
Real Lookup time;

HandleCrash:{
  
  In Boolean IgnoreCrash;
  In String CheckpointName, Args;
  InOut String Info;
  
  // TODO: Currently, ordering a new execution of Name (with IgnoreCrash=true) means previously crashed
  // executions will be ignored since the successful new execution will be the most recent.
  // Is this OK?
  Integer latest_start_boot;
  Integer latest_end_boot;
  Boolean checked_latest = false;
  Boolean compatible_time = true;
    
  ExitCondition !Lookup(DidCrash) // Didn't crash
    || IgnoreCrash // We want to start a new execution even if we crashed during the old one
    || (checked_latest && latest_start_boot == latest_end_boot) // Crashed, but completed most recent call with these args
    || (checked_latest && !isKnown(latest_start_boot)) //Never started
    || !compatible_time; // Last execution not relevant

  
  latest_start_boot = Lookup(CheckpointWhen(CheckpointName+Args));
  latest_end_boot = Lookup(CheckpointWhen(CheckpointName+"End"+Args));
  checked_latest = true;
  // Determine if the last execution was relevant
  LibraryCall CompatibleTime(First=Lookup(CheckpointTime(CheckpointName+Args,latest_start_boot)),
                 Second=Lookup(time,1),
                 CheckpointName=CheckpointName,
                 Args=Args,
                 Result=compatible_time);

  // From here on out, we can assume we've crashed
  Info = Lookup(CheckpointInfo(CheckpointName+Args,latest_start_boot));
}
