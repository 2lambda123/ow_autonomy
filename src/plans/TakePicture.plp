// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Take a picture with the stereo camera.

Real Lookup time;
Boolean Lookup ImageReceived;
Command take_picture();
Command log_info (...);
Command log_error (...);

TakePicture:
{
  Real TimeOut = 5.0;  // unitless, made up
  Boolean GotImage = false;

  PostCondition GotImage;

  // Command the operation
  take_picture();

  // Wait for result
  WaitForPicture:
  {
    SkipCondition Lookup(time, 1) - WaitForPicture.WAITING.START > TimeOut;
    StartCondition Lookup(ImageReceived);
    log_info ("Raw image data received.");
    GotImage = true;
  }

  // Report image failure if needed
  ReportImageFailure:
  {
    SkipCondition GotImage;
    log_error ("Did not receive raw image data.");
  }
}
