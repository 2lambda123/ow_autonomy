// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// A test plan that detects joint overtorque.
// NOTE: runs indefinitely.  Autonomy node must be killed to terminate plan.

#include "plexil_defs.h"
#include "plan-interface.h"

Boolean Lookup HardTorqueLimitReached (String joint_name);
Boolean Lookup SoftTorqueLimitReached (String joint_name);

TorqueTest:
{
  Boolean Digging = true;
  String JointNames[NUM_JOINTS] = #("AntennaPan" "AntennaTilt" "DistalPitch"
                                    "HandYaw" "ProximalPitch" "ScoopYaw"
                                    "ShoulderPitch" "ShoulderYaw");

  log_info ("Beginning over-torque test...");
  DigAndMonitor: Concurrence
  {
    Dig:
    {
      // This node attempts to dig too deep (note -1 Z target), in an effort to
      // overtorque one or more arm joints.
      guarded_move (2, 0, 0.3, 0, 0, 1, 0.5);
      WaitForGuardedMove:
      {
        StartCondition Lookup (Running ("GuardedMove"));
        EndCondition Lookup (Finished ("GuardedMove"));
      }
      SynchronousCommand publish_trajectory() Timeout 20.0, 0.1;
      
      // WaitForPublish:
      // {
      //   StartCondition Lookup (Running ("PublishTrajectory"));
      //   EndCondition Lookup (Finished ("PublishTrajectory"));
      // }
      Digging = false;
    }

    MonitorTorque:
    {
      RepeatCondition Digging;
      for (Integer i = 0; i < NUM_JOINTS; i+1) {
        if (Lookup(HardTorqueLimitReached(JointNames[i]))) {
          log_error ("Joint ", JointNames[i], " exceeding its hard limit.");
        }
        elseif (Lookup(SoftTorqueLimitReached(JointNames[i]))) {
          log_warning ("Joint ", JointNames[i], " exceeding its soft limit.");
        }
        endif;
      }
      Wait 1;
    }
  }
  log_info ("Over-torque test finished.");
}
