#include "OceanWorldDefs.h"

// A test plan that detects joint overtorque.
// TODO: make self-terminating

LibraryAction TakePanorama (In Real TiltLo, In Real TiltHi,
                            In Real PanLo, In Real PanHi,
                            In Real HorizOverlap, In Real VertOverlap);
Boolean Lookup HardTorqueLimitReached (String joint_name);
Boolean Lookup SoftTorqueLimitReached (String joint_name);
Command MoveGuarded (Real target_x, Real target_y, Real target_z,
                     Real surf_norm_x, Real surf_norm_y, Real surf_norm_z,
                     Real offset_dist, Real overdrive_dist,
                     Boolean delete_prev_traj, Boolean retract);
Command log_info (...);
Command log_warning (...);
Command log_error (...);

TorqueTest: Concurrence
{
  String JointNames[NUM_JOINTS] = #("Pan" "Tilt" "DistalPitch" "HandYaw"
                                    "ProximalPitch" "ScoopYaw" "ShoulderPitch"
                                    "ShoulderYaw");

  // Exercise all the joints!
  LibraryCall TakePanorama (TiltLo = 0, TiltHi = 20,
                            PanLo = 90, PanHi = 110,
                            HorizOverlap = 2,
                            VertOverlap = 2);
  MoveGuarded (2, 0, 1, 0, 0, 1, 0.2, 0.2, true, true);

  MonitorTorque:
  {
    Repeat true;
    for (Integer i = 0; i < NUM_JOINTS; i+1) {
      if (Lookup(HardTorqueLimitReached(JointNames[i]))) {
        log_error ("Joint ", JointNames[i], " exceeding its hard limit.");
      }
      elseif (Lookup(SoftTorqueLimitReached(JointNames[i]))) {
        log_warning ("Joint ", JointNames[i], " exceeding its soft limit.");
      }
      endif;
    }
    Wait 1;
  }
}
