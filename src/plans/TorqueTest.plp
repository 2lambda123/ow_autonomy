#include "OceanWorldDefs.h"

// A test plan that detects joint overtorque.
// TODO: make self-terminating

LibraryAction TakePanorama (In Real TiltLo, In Real TiltHi,
                            In Real PanLo, In Real PanHi,
                            In Real HorizOverlap, In Real VertOverlap);
LibraryAction MoveGuarded ();

Boolean Lookup HardTorqueLimit (String joint_name);
Boolean Lookup SoftTorqueLimit (String joint_name);
Command log_info (...);
Command log_warning (...);
Command log_error (...);

TorqueTest: Concurrence
{
  String JointNames[NUM_JOINTS] = #("Pan" "Tilt" "DistalPitch" "HandYaw"
                                    "ProximalPitch" "ScoopYaw" "ShoulderPitch"
                                    "ShoulderYaw");
  
  // Exercise all the joints!
  LibraryCall TakePanorama (TiltLo = 0, TiltHi = 20,
                            PanLo = 90, PanHi = 110,
                            HorizOverlap = 2,
                            VertOverlap = 2);
  LibraryCall MoveGuarded();

  MonitorTorqueLimits:
  {
    Repeat true;
    for (Integer i = 0; i < NUM_JOINTS; i+1) {
      if (Lookup(HardTorqueLimit(JointNames[i]))) {
        log_error ("Joint ", JointNames[i], " exceeding its hard limit.");
      }
      elseif (Lookup(SoftTorqueLimit(JointNames[i]))) {
        log_warning ("Joint ", JointNames[i], " exceeding its soft limit.");
      }
      endif;
    }
    Wait 1;
  }
}
