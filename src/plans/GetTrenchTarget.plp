// Copyright (c) 2018-2020, United States Government as represented by the
// Administrator of the National Aeronautics and Space Administration. All
// rights reserved.

// Get Trench Target Decision plan.

Command log_info (String);
Command request_ground_decision();
Command update_trench_target();
Command timeout_use_onboard_target();
Boolean Lookup Waiting;
Boolean Lookup WaitForGroundTimeout;
LibraryAction DownlinkImage;
LibraryAction DownlinkTarget;

GetTrenchTarget: Sequence
{

  LibraryCall DownlinkImage;
  LibraryCall DownlinkTarget;

  log_info ("Waiting for FWD link from Ground Control.");
  request_ground_decision();

  GetDecisionFromGround: Concurrence
  {
    WaitingForGroundTimeout: {
      StartCondition Lookup(WaitForGroundTimeout);
      SkipCondition UpdateTrenchTarget.state == FINISHED;
      log_info ("Request timed-out, using onboard target.");
      timeout_use_onboard_target();
    }

    UpdateTrenchTarget: {
      StartCondition NOT Lookup(Waiting);
      update_trench_target();
      log_info ("Trench target updated.");
      log_info ("Resuming plan.");
    }
  }
}
